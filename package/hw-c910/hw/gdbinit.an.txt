set confirm off
set height 0

# Invalidate & Clear IBP BTB BHT ICache & DCache
set $mcor = 0x70013

# Enable L2 Cache
set $mccr2 = 0xe0010009

# Enable L1 Cache
set $mhcr = 0x11ff

# Enable CPU Features
set $mxstatus = 0x638000
set $mhint = 0x16e30c

# 0x00000000 ~ 0xf0000000 DDR, TOR rwx
set $pmpaddr0 = 0xf0000000 >> 2

# 0xf0000000 ~ 0xf8000000 plic, NAPOT rw
set $pmpaddr1 = 0xf0000000 >> 2 | ((0x8000000 - 1) >> 3)

# 0xfff73000 ~ 0xfff74000 serial, NAPOT rw
set $pmpaddr2 = 0xfff73000 >> 2 | ((0x1000 - 1) >> 3)

# 0xfffc0000 ~ 0xfffc2000 gmac, NAPOT rw
set $pmpaddr3 = 0xfffc0000 >> 2 | ((0x2000 - 1) >> 3)

# 0x00000000 ~ 0x10000000 NAPOT no access
set $pmpaddr4 = 0x00000000 >> 2 | ((0x10000000 - 1) >> 3)

# 0x10000000 ~ 0x10000000000 TOR no access
set $pmpaddr5 = 0xffffffffff >> 2

# Be care! we must put background deny entries in the end of
# pmpaddrx with lowest priority and set lock bit for m state deny.
# Access needn't lock bit for the m state.
set $pmpcfg0 = 0x88981b1b1b0f

set $opensbi_addr = 0x00000000
set $vmlinux_addr = $opensbi_addr + 0x00200000
set $rootfs_addr  = $opensbi_addr + 0x02000000
set $dtb_addr  = $rootfs_addr - 0x00100000

# Load rootfs
restore ../rootfs.cpio.gz binary $rootfs_addr

# Load dtb
restore hw.dtb binary $dtb_addr
set $a1 = $dtb_addr

# Load kernel
restore ../Image binary $vmlinux_addr

# Load opensbi
restore fw_jump_0G.bin binary $opensbi_addr
set $pc = $opensbi_addr
