set confirm off
set height 0

# Enable smp
set $msmpr = 0x1

# Invalidate & Clear IBP BTB BHT ICache & DCache
set $mcor = 0x70013

# Enable L2 Cache
set $mccr2 = 0xe0010009

# Enable L1 Cache
set $mhcr = 0x11ff

# Enable CPU Features
set $mxstatus = 0x638000
set $mhint = 0x16e30c

# L2PMP
set *0x3f4020108 = 0x3f0000000 >> 12
set *0x3f402010c = 0x400000000 >> 12
set *0x3f4020100 = 0x0 >> 12
set *0x3f4020104 = 0x200000000 >> 12
set *0x3f4020000 = 0b1100001111000111

# Reset DW AXI DMAC
set *0x3fff78050 |= (1 << 0)

set $opensbi_addr = 0x00000000
set $vmlinux_addr = $opensbi_addr + 0x00200000
set $rootfs_addr  = $opensbi_addr + 0x02000000
set $dtb_addr  = $rootfs_addr - 0x00100000

# Load rootfs
restore ../rootfs.cpio.gz binary $rootfs_addr

# Load dtb
restore hw.dtb binary $dtb_addr
set $a1 = $dtb_addr

# Load kernel
restore ../Image binary $vmlinux_addr

# Load opensbi
restore fw_jump_0G.bin binary $opensbi_addr
set $pc = $opensbi_addr
