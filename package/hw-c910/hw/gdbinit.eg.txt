set confirm off

# Invalidate & Clear IBP BTB BHT ICache & DCache
set $mcor = 0x70013

# Enable L2 Cache
set $mccr2 = 0xe0010009

# Enable L1 Cache
set $mhcr = 0x11ff

# Enable CPU Features
set $mxstatus = 0x638000
set $mhint = 0x16e30c

# 0x1fba4000 ~ 0x1fba5000 serial NAPOT rw
set $pmpaddr0 = 0x1fba4000 >> 2 | ((0x1000 - 1) >> 3)

# 0x1fb10000 ~ 0x1fb12000 gmac NAPOT rw
set $pmpaddr1 = 0x1fb10000 >> 2 | ((0x2000 - 1) >> 3)

# 0x20000000 ~ 0x28000000 plic & clint NAPOT rw
set $pmpaddr2 = 0x20000000 >> 2 | ((0x08000000 - 1) >> 3)

# 0xc0000000 ~ 0x100000000 DDR NAPOT rwx
set $pmpaddr3 = 0xc0000000 >> 2 | ((0x40000000 - 1) >> 3)

# 0x00000000 ~ 0x10000000 NAPOT no access
set $pmpaddr4 = 0x00000000 >> 2 | ((0x10000000 - 1) >> 3)

# 0x10000000 ~ 0x10000000000 TOR no access
set $pmpaddr5 = 0xffffffffff >> 2

# Be care! we must put background deny entries in the end of
# pmpaddrx with lowest priority and set lock bit for m state deny.
# Access needn't lock bit for the m state.
set $pmpcfg0 = 0x88981f1b1b1b

set $bbl_addr = 0xc0000000
set $vmlinux_addr = $bbl_addr + 0x00200000
set $rootfs_addr  = $bbl_addr + 0x02000000
set $dtb_addr  = $rootfs_addr - 0x00100000

# Load rootfs
restore ../rootfs.cpio.gz binary $rootfs_addr

# Load dtb
restore hw.dtb binary $dtb_addr
set $a1 = $dtb_addr

# Load kernel
restore ../Image binary $vmlinux_addr

# Load bbl
restore fw_jump_3G.bin binary $bbl_addr
set $pc = $bbl_addr
