set endian little

# Invalid L1-cache include I/Dcache
set $cr17 = (1<<4) | 3 | (7<<16)

# Setup MMU MSA0 reg, format:
# 31 - 29 | 28 - 9 | 8 | 7 | 6 | 5 | 4 | 3 | 2 | 1 | 0
#   BA     Reserved  SH  WA  B   SO SEC  C   D   V
set $mcr30 = 0x000001ce

# Setup CPU features regs
# Enable MMU, Icache, Dcache, Return Stack, BPB, BTB, IBTB ...
set $cr31 = 0x650c
set $cr18 = 0x1586d

# Invalid L2 cache by 'exec l2cache.iall'
set *0x8f000000=0x9820c100
set $pc=0x8f000000
si

# Enable L2 cache
set $cr23 = 0x9

#################################
# Pass dtb to linux and jmp linux
#################################

# Store dtb to memory
restore hw.dtb binary 0x8F000000

# Pass dtb memory addr to reg as the argument to kernel
set $r1 = 0x8F000000

# load vmlinux to memory by elf format
restore ../Image binary 0x80000000

# Setup PC to run
set $pc = 0x80000000

# Sync I/Dcache before run kernel
set $cr17 = 0x33
