set endian little
set confirm off

# Invalid L1-cache include I/Dcache
set $cr17 = (1<<4) | 0x3

# Setup MMU MSA0 reg, format:
# 31 - 29 | 28 - 9 | 8 | 7 | 6 | 5 | 4 | 3 | 2 | 1 | 0
#   BA     Reserved  SH  WA  B   SO SEC  C   D   V
set $mcr30 = 0xc000000e

# Setup CPU features regs
# Enable MMU, Icache, Dcache, Return Stack, BPB, BTB, IBTB ...
set $cr31 = 0xfff
set $cr18 = 0x187d

# Invalid L2 cache
set $cr24 = (1<<4)
# Enable L2 cache
set $cr23 = 0x1018

set $vmlinux_addr = 0x80000000
set $rootfs_addr = $vmlinux_addr + 0x01000000
set $dtb_addr    =  $rootfs_addr - 0x00100000

# Load rootfs
restore ../rootfs.cpio.gz binary $rootfs_addr

# Load dtb
restore hw.dtb binary $dtb_addr
set $r1 = $dtb_addr

# Load kernel
restore ../Image binary $vmlinux_addr
set $pc = $vmlinux_addr

# Sync I/Dcache before run kernel
set $cr17 = 0x33
