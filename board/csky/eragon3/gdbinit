# Set the JTAG address according to C-SKY debugserver settings
tar jtag jtag://192.168.0.88:1025

# Set gdb environment
set height 0

# Setup MMU MSA0 reg
set $mcr30 = 0xe

# Invalid L1-cache include I/Dcache
set $cr31=0
set $cr17 = (1<<4) | 0x3
# Enable L1 cache and MMU and other CPU features
set $cr18 = 0xc87d

# Invalid L2 cache
set $cr24 = (1<<4)
# Enable L2 cache
set $cr23 = 0x1018

# Pass the vmlinux entry address to tee_sim.elf
set $r0 = 0x81200000

# Pass the devicetree blob(dtb) store address to Linux kernel
set $r1 = 0x8F000000

# Store the devicetree blob(dtb) file to the store address above
restore output/images/eragon3.dtb binary 0x8F000000

file board/csky/eragon3/tee_sim.elf
load

# Load the vmlinux
file output/build/linux-4.16.2/vmlinux
load

# Sync I/Dcache before run kernel
set $cr17 = 0x33

set $pc = 0x80100000

